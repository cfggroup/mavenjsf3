/*
 * Copyright 2014-2016 Riccardo Massera (TheCoder4.Eu)
 *
 * This file is part of BootsFaces.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//Compile BootsFaces Less files to CSS

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:2.7.4'
        classpath 'com.bertramlabs.plugins:less-asset-pipeline:2.7.4'
    }
}

/*plugins {
//  id "com.bertramlabs.asset-pipeline" version "2.5.0"
}*/

apply plugin: 'com.bertramlabs.asset-pipeline'

Properties props = new Properties()
props.load(new FileInputStream("${projectDir}/../build.properties"))
ext.BootstrapVersion = props['bootstrap.version']
ext.themes=props['bootswatch.theme'].replaceAll("\\s","").tokenize(',')
println 'Themes('+themes.size+'):'+props['bootswatch.theme']

assets {
  minifyJs = true
  minifyCss = true
  enableSourceMaps = false
  configOptions = [:]

  includes = ['default/bs-*.less']
  themes.each() { name -> includes.add(name+'/bs-*.less') }
  
  excludes = ['**/*.less','**/mixins/*.less'] //Exclude mixins

  // Can add custom asset locations (directories or individual jar files)
  from "${buildDir}/process/less"
  from "${buildDir}/process/bootstrap"
  from "${buildDir}/process/bsf"
}

assetCompile.dependsOn assetClean

task cleanAssetDir(type: Delete) {
     delete fileTree(dir: "${buildDir}/assets", includes: ['*.gz', '*-*.js'])
     //delete fileTree(dir: "${buildDir}/assets/less", includes: ['*.gz', 'bs-*-*.css', 'bsf-*.css', '*-*.js'])
     delete fileTree(dir: "${buildDir}/assets/default", includes: ['*.gz', 'bs-*-*.css', 'bsf-*.css', '*-*.js'], excludes: ['bs-progress-bars.css', 'bs-list-group.css'])
     delete fileTree(dir: "${buildDir}/assets", includes: ['*.gz', 'bsf-*.*'])
}

assetCompile.doLast {
   cleanAssetDir.execute()
   String license = "/*! Bootstrap v"+BootstrapVersion+" (http://getbootstrap.com) | Copyright 2011-2016 Twitter, Inc. | Licensed under MIT */\n"
   println license
   
   FileTree tree = fileTree("${buildDir}/assets/default") {
     include '**/*.css'
   }
   // Iterate over the contents of the tree
   tree.each {
     File css -> println 'Adding license to '+css
     String file = css
     String contents = css.getText( 'UTF-8' )
     contents = license+contents
     new File( file ).write( contents, 'UTF-8' )
   }
}
